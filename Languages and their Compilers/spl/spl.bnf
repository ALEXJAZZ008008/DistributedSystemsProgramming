<program> ::=	<identifier> ":" <block> ENDP <identifier> "."

<block>	::=	CODE <statement_list>
			| DECLARATIONS <declaration_list> CODE <statement_list>

<declaration_list>	::=	<declaration>
					| <declaration> <declaration_list>

<declaration>	::=	<identifier_list> OF TYPE <type> ";"

<type>	::=	REAL
			| INTEGER
			| CHARACTER

<statement_list>	::=	<statement>
						| <statement> ";" <statement_list>

<statement>	::=	<do_statement>
				| <if_statement>
				| <for_statement>
				| <read_statement>
				| <while_statement>
				| <write_statement>
				| <assignment_statement>

<assignment_statement>	::=	<expression> "->" <identifier>

<if_statement>	::=	IF <conditional> THEN <statement_list> ENDIF
					| IF <conditional> THEN <statement_list> ELSE <statement_list> ENDIF

<do_statement>	::=	DO <statement_list> WHILE <conditional> ENDDO

<while_statement>	::=	WHILE <conditional> DO <statement_list> ENDWHILE

<for_statement>	::=	FOR <identifier> IS <expression> BY <expression> TO <expression> DO <statement_list> ENDFOR

<write_statement>	::=	NEWLINE
						| WRITE "(" <output_list> ")"

<read_statement>	::=	READ "(" <identifier> ")"

<output_list>	::=	<value>
					| <value> "," <output_list>

<conditional>	::=	NOT <conditional>
					| <expression> <comparator> <expression>
					| <conditional> OR <conditional>
					| <conditional> AND <conditional>

<comparator>	::=	"="
					| "<"
					| ">"
					| "<="
					| ">="
					| "<>"

<expression>	::=	<term>
					| <term> "+" <expression>
					| <term> "-" <expression>

<term>	::=	<value>
			| <value> "*" <term>
			| <value> "/" <term>

<value>	::=	<constant>
			| <identifier>
			| "(" <expression> ")"

<constant>	::=	<number_constant>
				| <character_constant>

<character_constant>	::=	"'" <character> "'"

<number_constant>	::= <number>
						| "-" <number>
						| <number> "." <number>
						| "-" <number> "." <number>

<number>	::=	<digit>
				| <number> <digit>

<identifier_list>	::=	<identifier>
						| <identifier_list> "," <identifier>
				
<identifier>	::=	<character>
					| <identifier> <character>
					| <identifier> <digit>

<character>	::=	a | b | c | d | e | f | g | h | i | j | k | l | m | n | o | p | q | r | s | t | u | v | w | x | y | z
				| A | B | C | D | E | F | G | H | I | J | K | L | M | N | O | P | Q | R | S | T | U | V | W | X | Y | Z

<digit>	::=	0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9